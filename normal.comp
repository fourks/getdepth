#version 430 core

// ワークグループのサイズ
layout (local_size_x = 1, local_size_y = 1) in;

// カメラ座標を入力するイメージユニット
layout (rgba32f) readonly uniform image2D point;

// 法線ベクトルを出力するバッファオブジェクト
layout (std430) writeonly buffer Normal
{
  vec4 normal[];
};

void main(void)
{
  // 画素位置
  const ivec2 p = ivec2(gl_GlobalInvocationID.xy);

  // 近傍の勾配を求める
  const vec3 vx = vec3(imageLoad(point, p + ivec2(1, 0)) - imageLoad(point, p + ivec2(-1, 0)));
  const vec3 vy = vec3(imageLoad(point, p + ivec2(0, 1)) - imageLoad(point, p + ivec2(0, -1)));

  // 頂点インデックス
  const int i = p.y * imageSize(point).x + p.x;

  // 勾配からから法線ベクトルを求める
  normal[i] = vec4(cross(vx, vy), 0.0);
}
