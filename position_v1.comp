#version 430 core

// ワークグループのサイズ
layout(local_size_x = 16, local_size_y = 20) in;

// 入力
layout (binding = 0, r16ui) readonly uniform uimage2D depth;

// イメージのサイズ - 1
const ivec2 ds = imageSize(depth) - 1;

// 出力
layout (binding = 1, rgba32f) writeonly uniform image2D position;

// フィルタのサイズ
const ivec2 filterSize = ivec2(5, 5);

// フィルタの中心位置
const ivec2 filterOffset = filterSize / 2;

// 一度に処理する領域のサイズ
const ivec2 tileSize = ivec2(gl_WorkGroupSize) - ivec2(0, filterOffset.y * 2);

// 近傍を含む領域のサイズ
const ivec2 neighborhoodSize = tileSize + filterOffset * 2;

// バイラテラルフィルタの距離に対する重みの Shader Storage Buffer Object
layout(binding = 2, std430) buffer Weight
{
  readonly float columnWeight[filterSize.y];
  readonly float rowWeight[filterSize.x];
  readonly float variance;
};

// スケール
uniform vec2 scale;

// 処理する領域の近傍を含めたコピー
shared float pixel[neighborhoodSize.y][neighborhoodSize.x];
shared float row[neighborhoodSize.y][tileSize.x]; 

// 対象画素の値
float base;

// 重み付き画素値の合計と重みの合計を求める
vec2 f(const in float c, const in float w)
{
  const float d = c - base;
  const float e = exp(-0.5 * d * d / variance) * w;
  return vec2(c * e, e);
}

// インデックスがイメージの領域から外れないようにする
ivec2 clampLocation(ivec2 xy)
{
  return clamp(xy, ivec2(0), ds);
}

// 他のスレッドの共有メモリへのアクセス完了と他のワークグループの処理完了を待つ
void retirePhase()
{
  memoryBarrierShared();
  barrier();
}

void main(void)
{
  // ワークグループが処理する領域の基準位置
  const ivec2 tile_xy = ivec2(gl_WorkGroupID);

  // スレッドが処理する画素のワークグループにおける相対位置
  const ivec2 thread_xy = ivec2(gl_LocalInvocationID);

  // イメージ上の画素位置
  const ivec2 pixel_xy = tile_xy * tileSize + thread_xy - ivec2(0, filterOffset.y);

  // スレッドが処理する画素位置
  const uint x = thread_xy.x;
  const uint y = thread_xy.y;

  // 処理する領域をコピーする
  for (int i = 0; i < neighborhoodSize.x; i += tileSize.x)
  {
    if (x + i < neighborhoodSize.x)
    {
      const ivec2 read_at = clampLocation(pixel_xy + ivec2(i - filterOffset.x, 0));
      pixel[y][x + i] = float(imageLoad(depth, read_at).r);
    }
  }

  // 他のスレッドの共有メモリへのアクセス完了と他のワークグループの処理完了を待つ
  retirePhase();

  // 対象画素の値を基準値とする
  base = pixel[y][x + filterOffset.x];

  // 対象画素の値とその重みのペアを作る
  vec2 csum = vec2(0.0);

  // 列方向の重み付け和を求める
  for(int i = 0; i < filterSize.x; ++i)
  {
    csum += f(pixel[y][x + i], rowWeight[i]);
  }

  // デプス値を取り出す
  row[y][x] = csum.r / csum.g;

  // 他のスレッドの共有メモリへのアクセス完了と他のワークグループの処理完了を待つ
  retirePhase();

  if (y < tileSize.y)
  {
    // 対象画素の値とその重みのペアを作る
    vec2 csum = vec2(0.0);

    // 行方向の重み付け和を求める
    for (int j = 0; j < filterSize.y; ++j)
    {
      csum += f(row[y + j][x], columnWeight[j]);
    }

    // デプス値を取り出す
    const float z = -0.001 * csum.r / csum.g;

    // 結果を保存する画素位置を求める
    const ivec2 pixel_xy = tile_xy * tileSize + thread_xy; 

    // デプス値からカメラ座標値を求める
    imageStore(position, pixel_xy, vec4((vec2(pixel_xy) / vec2(ds) - 0.5) * scale * z, z, 1.0));
  }
}
